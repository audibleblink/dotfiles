# vim: ft=zsh foldmarker=[[[,]]] foldlevelstart=0 foldmethod=marker 

# Navigation [[[
setopt auto_pushd
setopt pushd_ignore_dups
setopt pushdminus
setopt auto_cd
# case-insensitive globbing (used in pathname expansion)
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
# sort completions by when they were last used
zstyle ':completion:*:*:*:*' file-sort modification
# enable cycling through completions 
zstyle ':completion:*' menu select


# ]]]
# History [[[
setopt append_history
setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_dups # ignore duplication command history list
setopt hist_ignore_space
setopt hist_verify
setopt inc_append_history
setopt share_history # share command history data
HISTSIZE=100000
SAVEHIST=100000
# ]]]
# Key Bindings [[[
# Make sure that the terminal is in application mode when zle is active, since
# only then values from $terminfo are valid
if (( ${+terminfo[smkx]} )) && (( ${+terminfo[rmkx]} )); then
  function zle-line-init() {
    echoti smkx
  }
  function zle-line-finish() {
    echoti rmkx
  }
  zle -N zle-line-init
  zle -N zle-line-finish
fi

# start typing + [Up-Arrow] - fuzzy find history forward
if [[ "${terminfo[kcuu1]}" != "" ]]; then
  autoload -U up-line-or-beginning-search
  zle -N up-line-or-beginning-search
  bindkey "${terminfo[kcuu1]}" up-line-or-beginning-search
fi

# start typing + [Down-Arrow] - fuzzy find history backward
if [[ "${terminfo[kcud1]}" != "" ]]; then
  autoload -U down-line-or-beginning-search
  zle -N down-line-or-beginning-search
  bindkey "${terminfo[kcud1]}" down-line-or-beginning-search
fi

# Filter through multiple ssh config files and hosts
function get-ssh() {
  # read the selected Host entry
  IFS=: read host <<< $(grep '^Host [^*]' ${HOME}/.ssh/*config | sed 's/:Host//' | tr -d '"'| fzf)
  # set the local buffer to the new command
  LBUFFER="${LBUFFER} -F ${host}"
  local ret=$?
  zle reset-prompt
  return $ret
}
zle     -N   get-ssh
bindkey '^S' get-ssh

# Creates a new tmux window with a floating terminal
function floats() {
  tmux popup -E 
}
zle     -N   floats
bindkey '^[i' floats

function prepend-sudo {
  if [[ $BUFFER != "sudo "* ]]; then
    BUFFER="sudo $BUFFER"; CURSOR+=5
  fi
}
zle -N prepend-sudo
bindkey -M vicmd s prepend-sudo
# ]]]

plugins=(
  # evalcache
  vi-mode
  zsh-autopair
  gpg
  todo
  atuin
  fuck
  # pentest
  zsh-syntax-highlighting
)

source "${ZDOTDIR}/aliases.zsh"      # Sources custom aliases
source $ZDOTDIR/sourcer.zsh          # Sets up fpaths and plugins

[ -f ~/.zshrc_local ] && source ~/.zshrc_local

command -v starship > /dev/null && eval "$(starship init zsh)"

if [ -f "${XDG_DATA_HOME}"/gvm/scripts/gvm-default ]; then
  GVM_ROOT="${XDG_DATA_HOME}/gvm"
  source "${XDG_DATA_HOME}"/gvm/scripts/gvm-default
  typeset -U PATH path
fi


# vim: ft=zsh foldmarker=[[[,]]] foldlevelstart=1 foldmethod=marker 
# zmodload zsh/zprof

# Navigation [[[
setopt auto_pushd
setopt pushd_ignore_dups
setopt pushdminus
setopt globstarshort
setopt cd_silent
# setopt auto_cd
# case-insensitive globbing (used in pathname expansion)
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
# sort completions by when they were last used
zstyle ':completion:*:*:*:*' file-sort modification
# enable cycling through completions 
zstyle ':completion:*' menu select
# don't show $cdpath directories in completion
zstyle ':completion:*:complete:(cd|pushd):*' tag-order 'local-directories named-directories'

export cdpath=( . ${XDG_CONFIG_HOME} ${HOME}/.local ${HOME} )

# ]]]

# History [[[
export HISTFILE="${XDG_STATE_HOME}/zsh_history"
setopt append_history
setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_dups # ignore duplication command history list
setopt hist_ignore_space
setopt hist_verify
setopt inc_append_history
setopt share_history # share command history data
HISTSIZE=100000
SAVEHIST=100000
# ]]]

# Functions and Aliases [[[
{{- if (eq .chezmoi.os "darwin")}}
if [[ -z "$HOMEBREW_PREFIX" ]]; then
		eval "$(brew shellenv)"
fi
{{- end }}
source "${ZDOTDIR}/aliases.zsh"  # Sources custom aliases

# Reset fpath and add custom functions and completions
typeset -U fpath
# Add local completions and function directories to fpath
# (N/) - nullglob for directories only, returns nothing if no match
fpath=($ZDOTDIR/{completions,functions/**(N/)} $fpath)
# Autoload all function files in functions directories
# (N.:t) - nullglob, basename only, return nothing if no match
autoload -Uz $ZDOTDIR/functions/**(N.:t)
# ]]]

# Plugins [[[
{{- if (.personal) }}
lazy no-op
lazy todo
lazy atuin
{{- end }}
# plugins added to stack are loaded in reverse order
lazy zsh-syntax-highlighting
lazy autopair 
lazy zsh-vi-mode
lazy substring-search
# Load key bindings via zsh-defer for better startup performance
zsh-defer bindings
# ]]]

# Compinit [[[
# Rebuild cache every 2 hours
autoload -U compinit
if [[ -n ~/.config/zsh/.zcompdump(#qNmh+2) ]]; then
		compinit                                                            
else
		compinit -C                                                         
fi
# ]]]

(( $+commands[starship] && ! $+functions[starship_zle-keymap-select] )) && eval "$(starship init zsh)"
(( $+commands[mise] && ! $+functions[_mise_hook] )) && zsh-defer -c 'eval "$(mise activate)"'
(( $+commands[tv] && ! $+functions[_tv__init_commands] )) && zsh-defer -c 'eval "$(tv init zsh)"'

[ -f ~/.zshrc_local ] && source ~/.zshrc_local

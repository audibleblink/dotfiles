# vim: ft=zsh foldmarker=[[[,]]] foldlevelstart=0 foldmethod=marker 
# zmodload zsh/zprof

# Navigation [[[
setopt auto_pushd
setopt pushd_ignore_dups
setopt pushdminus
# setopt auto_cd
# case-insensitive globbing (used in pathname expansion)
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
# sort completions by when they were last used
zstyle ':completion:*:*:*:*' file-sort modification
# enable cycling through completions 
zstyle ':completion:*' menu select
# don't show cdpath directories in completion
zstyle ':completion:*:complete:(cd|pushd):*' tag-order 'local-directories named-directories'

export cdpath=( . ${XDG_CONFIG_HOME}  ${HOME} )

# ]]]
# History [[[
setopt append_history
setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_dups # ignore duplication command history list
setopt hist_ignore_space
setopt hist_verify
setopt inc_append_history
setopt share_history # share command history data
HISTSIZE=100000
SAVEHIST=100000
# ]]]
# Key Bindings [[[
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down

# Ctrl+Z to toggle last job (likely nvim)
fancy-ctrl-z () {
  if [[ $#BUFFER -eq 0 ]]; then
    BUFFER="fg"
    zle accept-line
  else
    zle push-input
    zle clear-screen
  fi
}
zle -N fancy-ctrl-z
bindkey '^Z' fancy-ctrl-z

# Creates a new tmux window with a floating terminal
function floats() {
  tmux popup -E 
}
zle     -N   floats
bindkey '^[i' floats


# Prepend sudo to the current command
function prepend-sudo {
  if [[ $BUFFER != "sudo "* ]]; then
    BUFFER="sudo $BUFFER"; CURSOR+=5
  fi
}
zle -N prepend-sudo
bindkey -M vicmd ! prepend-sudo
# ]]]
# Functions and Aliases [[[
{{- if (eq .chezmoi.os "darwin")}}
eval "$(/opt/homebrew/bin/brew shellenv)"
{{- end }}
source "${ZDOTDIR}/aliases.zsh"  # Sources custom aliases

# Reset fpath and add functions and completions
fpath=($(zsh -l -c 'echo $fpath'))
fpath=($ZDOTDIR/{completions,functions} $fpath)
autoload -Uz $ZDOTDIR/functions/**/*
# ]]]
# Plugins [[[
plugins=(
  {{- if (.personal) }}
  # gpg
  # pentest
  todo
  atuin
  {{- end }}
  zsh-syntax-highlighting
  substring-search
  zsh-vi-mode
)

for plugin in $plugins; do
  zsh-defer source "${ZDOTDIR}/plugins/${plugin}/${plugin}.plugin.zsh"
done

# ]]]
# Compinit [[[
# Refresh compinit once every 24 hours
autoload -U compinit
() {
  if [[ $# -gt 0 ]]; then
    compinit
  else
    compinit -C
  fi
} ${ZDOTDIR}/.zcompdump(N.mh+24)
# ]]]

command -v starship > /dev/null && eval "$(starship init zsh)"
[ -f ~/.zshrc_local ] && source ~/.zshrc_local
